/////////////////////////////////////////////////////////////
///\file animationExporter.mel
///\brief fbx导出工具
///
///\author 桔
///\version 1.4.2
///\date  9:45 2022/4/6
///History:  导出动画工具
///
///////////////////////////////////////////////////////////////
global proc J_animationExporter()//主窗口
{
    if (`window -ex J_animationExporterWin`)
        deleteUI -window J_animationExporterWin;
    window -w 300 -h 380 -title "动画导出" J_animationExporterWin;
    //文件导出设置
    string $geoSetting[]={"SmoothingGroup","SmoothMesh","SelectionSets","Triangulate",
     "IncludeChildren","InputConnections","Skin","BlendShape" };
    string $geoSettingCheckBox[];

    tabLayout J_animationExporterTab;
        // formLayout  -numberOfDivisions 100  J_animationExporter_FromLayout0;
        //     frameLayout -cll 1 -cl 0 -l "模型选项"  J_animationExporter_frameLayout0;
        //         string $formLayout_Temp0=`formLayout -numberOfDivisions 100 `;
        //             for ($iItem=0;$iItem<size($geoSetting);$iItem++)
        //             {
        //                 $geoSettingCheckBox[$iItem]=`checkBox -label  $geoSetting[$iItem] `;
        //                 formLayout -e 
        //                 -ap $geoSettingCheckBox[$iItem] left 0 (1+50*($iItem%2))
        //                 -ap $geoSettingCheckBox[$iItem] right 0 (49+50*($iItem%2))
        //                 -af $geoSettingCheckBox[$iItem] top (15*floor($iItem/2)) $formLayout_Temp0;
        //             }
        //         setParent ..;    
        //     setParent ..;
        //     frameLayout -cll 1 -cl 0 -l "文件配置"  J_animationExporter_frameLayout1;
        //         button;
        //     setParent ..;
        // setParent ..;
        formLayout  -numberOfDivisions 100  J_animationExporter_FromLayout1;
            textScrollList  -sc ""  -allowMultiSelection 1  J_allCameraNode;
            textScrollList  -sc "" -allowMultiSelection 1 J_allRefNode;
            checkBox -label "曲线平滑" -v 1 J_animationExporter_chbox01;
            checkBox -label "曲线打平"  J_animationExporter_chbox02;
            checkBox -label "根骨归零"  J_animationExporter_chbox03;
            optionMenu -label "差值模式" J_animationExporter_optionMenu01;
            menuItem  -label "resample";
            menuItem  -label "euler";
            menuItem  -label "quaternion";

            button -l "导出相机" -c  "J_animationExporterExportCamBut" J_expCam;
            button -l "导出动画" -c  "J_animationExporterExportAniBut" J_expAni;
            textField    J_tex;

            button -l "批量导出相机" -c  "J_animationExporterExportBatBut(0)" J_expCamBat;
            button -l "批量导出动画" -c  "J_animationExporterExportBatBut(1)" J_expAniBat;

        setParent ..;
    tabLayout -edit
        //-tabLabel "J_animationExporter_FromLayout0" "模型资产" -tabLabel "J_animationExporter_FromLayout1" "动画导出" J_animationExporterTab;
        -tabLabel "J_animationExporter_FromLayout1" "动画导出" J_animationExporterTab;
    // formLayout -e 
    //     -ap J_animationExporter_frameLayout0 left 0 1
    //     -ap J_animationExporter_frameLayout0 right 0 99
    //     -af J_animationExporter_frameLayout0 top 0
        
    //     -ap J_animationExporter_frameLayout1 left 0 1
    //     -ap J_animationExporter_frameLayout1 right 0 99
    //     -ac J_animationExporter_frameLayout1 top 1 J_animationExporter_frameLayout0

    // J_animationExporter_FromLayout0;

    formLayout -e 
        -ap J_allRefNode left 1 1
        -ap J_allRefNode right 1 99
        -af J_allRefNode top 0
        -ap J_allRefNode bottom 0 40
        
        -ap J_allCameraNode left 1 1
        -ap J_allCameraNode right 1 99
        -ac J_allCameraNode top  1 J_allRefNode
        -af J_allCameraNode bottom 120
        
        -ap J_animationExporter_chbox01 left 0 1
        -ap J_animationExporter_chbox01 right 0 32
        -ac J_animationExporter_chbox01 top  2 J_allCameraNode
        -ap J_animationExporter_chbox02 left 0 33
        -ap J_animationExporter_chbox02 right 0 65
        -ac J_animationExporter_chbox02 top  2 J_allCameraNode
        -ap J_animationExporter_chbox03 left 0 66
        -ap J_animationExporter_chbox03 right 0 99
        -ac J_animationExporter_chbox03 top  2 J_allCameraNode

        -ap J_animationExporter_optionMenu01 left 0 1
        -ap J_animationExporter_optionMenu01 right 0 99
        -ac J_animationExporter_optionMenu01 top  2 J_animationExporter_chbox01

        -ap J_expCam left 0 1
        -ap J_expCam right 0 49
        -ac J_expCam top  2 J_animationExporter_optionMenu01

        -ap J_expAni left 0 50
        -ap J_expAni right 0 99
        -ac J_expAni top  2 J_animationExporter_optionMenu01
        
        -ap J_tex left 0 1
        -ap J_tex right 0 99
        -ac J_tex top  2 J_expAni

        -ap J_expCamBat left 0 1
        -ap J_expCamBat right 0 50
        -ac J_expCamBat top  2 J_tex

        -ap J_expAniBat left 0 51
        -ap J_expAniBat right 0 99
        -ac J_expAniBat top  2 J_tex
       
    J_animationExporter_FromLayout1;


    
    showWindow J_animationExporterWin;
    J_animationExporterInit();
    J_animationExporterRunScriptJob();
    python("JpyModules.public.J_deleteUnknownNode()");
    python("JpyModules.public.J_cleanVaccine_gene()");
}


//查文件路径,未保存,则以c:/temp为准
global proc string J_animationExporterGetFilePath()
{       
    python("import os");
    string $filePath=python("os.path.dirname(maya.cmds.file(query=True,sceneName=True))");
    if (size ($filePath)<3)
        return "c:/temp";
    return $filePath;
}
//获取文件名
global proc string J_animationExporterGetFileNameWithOutExtension()
{       
    python("import os");
    string $filePath=python("os.path.basename(maya.cmds.file(query=True,sceneName=True))[:-3]");
    if (size ($filePath)<3)
        return "temp";
    return $filePath;
}
//导出相机按钮逻辑
global proc J_animationExporterExportCamBut()
{
    string $selCam[]=`textScrollList  -q -si  J_allCameraNode`;
    //string $outPath=J_animationExporterGetFilePath()+"/"+J_animationExporterGetFileNameWithOutExtension()+"/";
    string $outPath=J_animationExporterGetFilePath()+"/cache/";
    sysFile -md $outPath;
    if (size($selCam)>0)
    {
        J_animationExporterExportCam($selCam);
        
        //导出后打开文件夹
        string $cmd="os.startfile(os.path.dirname(\""+$outPath+"\"))";
        python($cmd); 
    }
}

global proc J_animationExporterExportCam(string $cams[])
{    
    string $outPath=J_animationExporterGetFilePath()+"/cache/";

    if (size($cams)>0)
    {
        select $cams;        
        //记录关键帧
        int $startFrame=`playbackOptions -query -minTime`;
        int $endFrame=`playbackOptions -query -maxTime`+1;  
        bakeResults -t ($startFrame+":"+$endFrame) -s 0 -simulation true $cams;
        string $outfbx=python("JpyModules.animation.J_animationExporter.J_analysisCamName()")+".fbx";
        if ($outfbx=="")
        {
            print "摄像机名解析失败，使用相机名导出";
            string $sel[]=`ls -sl`;
            $outfbx=$sel[0]+".fbx";
        }
        $outfbx=$outPath+$outfbx;
        print ("相机导出："+$outfbx);
        //配置参数
        FBXResetExport ;
        FBXExportInAscii  -v true;
        FBXExportBakeComplexAnimation -v 1;    
        FBXExportBakeComplexStart -v $startFrame;
        FBXExportBakeComplexEnd -v $endFrame;
        FBXExportBakeResampleAnimation -v 1;
        FBXExportInAscii -v 1;
        FBXExportIncludeChildren -v 1;
        FBXExportSplitAnimationIntoTakes -clear;    
        FBXExportDeleteOriginalTakeOnSplitAnimation -v true;
        FBXExportSplitAnimationIntoTakes -v "cam" $startFrame $endFrame;
        FBXExport -f $outfbx -s ;
    }
}


//导出动画
global proc J_animationExporterExportAniBut()
{
    string $selRef[]=`textScrollList  -q -si  J_allRefNode`;
    for ($item in $selRef)
    {  
        python("JpyModules.animation.J_animationExporter.J_exportAnimationToAbc('"+$item +"')");
    }
    J_animationExporterExportCamBut();
    string $outPath=J_animationExporterGetFilePath()+"/cache/";
    //导出后打开文件夹
    string $cmd="os.startfile(os.path.dirname(\""+$outPath+"\"))";
    python($cmd); 
    
}
//批量导出动画
global proc J_animationExporterExportBatBut(int $state)
{
    string $file[]=`fileDialog2 -fileMode 2 `;
    string $mayaFiles[]=`getFileList -folder $file[0] -filespec "*.m?" `;
    string $filter=`textField -q -text J_tex`;

    for ($fItem in $mayaFiles)
    {
        if (`endsWith $fItem ".mb"` ||`endsWith $fItem ".ma"` )
        {
            file -f  -prompt false   -open ($file[0]+"/"+$fItem );
            string $allCamera[]=`ls -type "camera"`;
            string $sysCam[]={"|back","|front","|left","|persp","|side","|top"};
            string $allCamTr[]=`listRelatives  -fullPath -p $allCamera`;
            string $exportCam[]= `stringArrayRemove $sysCam $allCamTr `;
            J_animationExporterExportCam($exportCam);
            string $allRefFile[]=`file -q -r`;
            for ($item0 in $allRefFile)
            {   
                string $refNode=`referenceQuery -referenceNode $item0`; 
                python("JpyModules.animation.J_animationExporter.J_exportAnimationToAbc('"+$refNode +"')");
            }
        }
    }
}
//删除名字空间
global proc  J_animationExporterRemoveAllNameSpace()
{
    string $nameSpaces[]=`namespaceInfo -listOnlyNamespaces`;
    string $items[] = { "shared", "UI"};
    string $diff[] = stringArrayRemove($items, $nameSpaces);
    if(size($diff)>0)
    {
        for($i in $diff)
        {
            namespace -mergeNamespaceWithRoot -removeNamespace $i ;
            print ($i +"被删除\n");
        }
        J_animationExporterRemoveAllNameSpace();
    }
}
//窗口初始化
global proc J_animationExporterInit()
{
    string $allCamera[]=`ls -type "camera"`;
    string $allCamTr[]=`listRelatives  -fullPath -p $allCamera`;
    string $sysCam[]={"|back","|front","|left","|persp","|side","|top"};
    textScrollList  -e -ra  J_allCameraNode;
    textScrollList  -e -ra   J_allRefNode;
    for ($item0 in $allCamTr)
    {
        int $temp=0;
        for ($item1 in $sysCam)
        {
            if ($item0==$item1)$temp=1;
        }
        if ($temp==0)
        textScrollList  -e -a $item0  J_allCameraNode;
    }
    string $allRefFile[]=`file -q -r`;
    for ($item0 in $allRefFile)
    {   
        string $refNode=`referenceQuery -referenceNode $item0`;
        textScrollList  -e -a $refNode  J_allRefNode;
    }
}
//自动更新窗体
global proc J_animationExporterScriptJob()
{
    J_animationExporterInit();
    textScrollList -e -deselectAll  J_allCameraNode;
    string $allListCam[]=`textScrollList  -q -ai  J_allCameraNode`;
    string $sel[]=`ls -sl -allPaths -l`;
    for($i in $sel)
    {
        if(stringArrayContains( $i, $allListCam))
        textScrollList -e -si $i J_allCameraNode;
    }
}
//随选择修改窗体内容
global proc J_animationExporterRunScriptJob()
{
    int $sjId = `scriptJob -e "SelectionChanged" J_animationExporterScriptJob `;
    string $temp = ("scriptJob -k "+ $sjId);
    scriptJob -uid "J_animationExporterWin" $temp;

}
//选择摄像机
global proc J_animationExporterSelitem()
{
    string $allListCam[]=`textScrollList  -q -si  J_allCameraNode`;
    select $allListCam;
}
//J_animationExporter;